.options savecurrents

* PHILIPS BJT'S
.MODEL BC557A PNP(IS=2.059E-14 ISE=2.971f ISC=1.339E-14 XTI=3 BF=227.3 BR=7.69 IKF=0.08719 IKR=0.07646 XTB=1.5 VAF=37.2 VAR=11.42 VJE=0.5912 VJC=0.1 RE=0.688 RC=0.6437 RB=1 RBM=1 IRB=1E-06 CJE=1.4E-11 CJC=1.113E-11 XCJC=0.6288 FC=0.7947 NF=1.003 NR=1.007 NE=1.316 NC=1.15 MJE=0.3572 MJC=0.3414 TF=7.046E-10 TR=1m2 ITF=0.1947 VTF=5.367 XTF=4.217 EG=1.11)
.MODEL BC547A NPN(IS=1.533E-14 ISE=7.932E-16 ISC=8.305E-14 XTI=3 BF=178.7 BR=8.628 IKF=0.1216 IKR=0.1121 XTB=1.5 VAF=69.7 VAR=44.7 VJE=0.4209 VJC=0.2 RE=0.6395 RC=0.6508 RB=1 RBM=1 IRB=1E-06 CJE=1.61E-11 CJC=4.388p XCJC=0.6193 FC=0.7762 NF=1.002 NR=1.004 NE=1.436 NC=1.207 MJE=0.3071 MJC=0.2793 TF=4.995E-10 TR=1m2 ITF=0.7021 VTF=3.523 XTF=139 EG=1.11)

Vcc vcc 0 12.0
Vin in 0 0 ac 1.0 sin(0 10m 1k)
Rin in in2 100


* input coupling capacitor
Ci in2 base 0.1m

* bias circuit
R1 vcc base 55k
R2 base 0 10k

* gain stage
Q1 coll base emit BC547A
Rc vcc coll 1.1k
Re emit 0 200

* bypass capacitor
Cb emit 0 2.8m


* output stage
Q2 0 coll emit2 BC557A
Rout emit2 vcc 220

* output coupling capacitor
Co emit2 out 1.52m

* load
RL out 0 8

.op
.end

.control

* time analysis
tran 1e-5 1e-2
plot v(coll)
hardcopy vo1.eps vdb(coll)

* frequency analysis
ac dec 10 10 100MEG
plot vdb(coll)
plot vp(coll)
hardcopy vo1f.eps vdb(coll)
hardcopy vo3f.eps vp(coll)

plot vdb(out)
plot vp(out)
hardcopy vo2f.eps vdb(out)
hardcopy vo4f.eps vp(out)

meas AC gain MAX vdb(out)
let ref=gain-3

meas AC flow WHEN vdb(out)=ref
meas AC fup WHEN vdb(out)=ref CROSS=LAST

let bandwidth=fup-flow
let cost = 4486.628
let gain = abs(v(emit2)[26]/v(in)[26])
let merit = ((gain*bandwidth)/(cost*flow))
print merit

*input impedance in kohm
*print all

*plot (abs(v(in2)[26]/v(in)[26]/(-1000)))
let impedin = (v(in2)[26]/(v(in)[26]-v(in2)[26])/100 * 1000)
print impedin

echo "op_TAB"
print bandwidth
print fup
print flow
print gain
print merit
print cost
print impedin
echo "op_END"

echo "op_TAB3"
print vcc[0]
print in[0]
print in2[0]
print base[0]
print coll[0]
print emit[0]
print emit2[0]
print out[0]
echo "op_END3"


*need a different setup to measure output impedance
.endc 

